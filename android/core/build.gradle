plugins {
    alias libs.plugins.androidLibrary
    alias libs.plugins.jetbrainsKotlinAndroid
    alias libs.plugins.ktfmt
    alias libs.plugins.mavenPublish
    // The magic plugin that automates the cargo integration
    alias libs.plugins.cargo.ndk
}

android {
    namespace 'app.unimusic.sync'
    compileSdk 35
    ndkVersion "28.2.13676358"

    defaultConfig {
        minSdk 29
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.androidx.ktx
    implementation libs.androidx.appcompat

    // Essential! Note that UniFFI dictates the minimum supported version, and this may change with new releases
    // TODO: Migrate version to TOML (doesn't work). Likely related issue: https://github.com/gradle/gradle/issues/21267
    //noinspection UseTomlInstead
    implementation 'net.java.dev.jna:jna:5.15.0@aar'

    testImplementation libs.junit
    testImplementation libs.kotlinx.coroutines.test

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.androidx.test.espresso
}

cargoNdk {
    module = "../rust" // Directory containing Cargo.toml
    librariesNames = ["libunimusic_sync.so"]
    extraCargoBuildArguments = ["-p", "unimusic-sync"]
}

android.libraryVariants.all { variant ->
    def bDir = layout.buildDirectory.dir("generated/source/uniffi/${variant.name}/java").get()
    def generateBindings = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
        workingDir '../../rust'
        commandLine 'cargo', 'run', '-p', 'uniffi-bindgen', 'generate', '--library', '../android/core/src/main/jniLibs/arm64-v8a/libunimusic_sync.so', '--language', 'kotlin', '--out-dir', bDir

        dependsOn "buildCargoNdk${variant.name.capitalize()}"
    }

    variant.javaCompileProvider.get().dependsOn(generateBindings)

    tasks.named("compile${variant.name.capitalize()}Kotlin").configure {
        dependsOn generateBindings
    }

    tasks.named("connectedDebugAndroidTest").configure {
        dependsOn generateBindings
    }

    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir layout.buildDirectory.file("generated/source/uniffi/${variant.name}/java")
}

mavenPublishing {
    pom {
        name = "UniMusicSync"
        description = "Synchronisation library for UniMusic"
        inceptionYear = "2025"
        url = "https://github.com/UniMusic-app/unimusic-sync/"
        licenses {
            // TODO: License
        }
        scm {
            url = "https://github.com/UniMusic-app/unimusic-sync/"
            connection = "scm:git:git://github.com/UniMusic-app/unimusic-sync.git"
            developerConnection = "scm:git:ssh://git@github.com/UniMusic-app/unimusic-sync.git"
        }
    }
}

publishing {
    repositories {
        maven {
            name = "githubPackages"
            url = uri("https://maven.pkg.github.com/unimusic-app/unimusic-sync")
            credentials(PasswordCredentials)
        }
    }
}